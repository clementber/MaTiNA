%{
  #include <iostream>
  #include <string>
  #include "../time_automata/grammar.hpp"
  #include "yacc.tab.hpp"
  using namespace automate;

  yy::parser::symbol_type yylex (yy::parser::semantic_type *arg);

%}
%skeleton "lalr1.cc"
%require "3.0.4"

%defines
%define parser_class_name {parser}
%define api.token.constructor
%define api.value.type variant
%define parse.trace
%define parse.error verbose

%parse-param {automate::Automate* generate_automate}

%code requires
{
  class autom_driver;
}
// The parsing context.
%param { autom_driver& driver }
%locations
%initial-action
{
  // Initialize the initial location.
  @$.begin.filename = @$.end.filename = &driver.file;
};
%code
{
# include "parsingDriver.hpp"
}

%token END 0 "end of file"
%token <double> NUMBER
%token <std::string> NAME

%type <automate::Automate*> autom
%type <std::vector<std::string>> names list_name
%type <std::vector<automate::Clock*>> clocks
%type <automate::DBM> conditions
%type <std::pair<automate::Clock*, std::pair<automate::Bound,automate::Bound>>> condition
%type <automate::Bound> borne_inf borne_sup

%token LBALISE COMMA LCROCHET RCROCHET LACOLADE RACOLADE EGALE
%token START_NODE END_NODE NLINE DOUBLE_SLASH NONE

%%
autom :
   nodes NLINE DOUBLE_SLASH NLINE arretes { $$ = generate_automate; }
  |nodes NLINE DOUBLE_SLASH NLINE arretes NLINE { $$ = generate_automate; }

nodes :
   nodes NLINE node { /*Nothing to do.*/ }
  |node { /*Nothing to do.*/ }

node :
   NAME START_NODE conditions {
     DBM conditions = $3;
     conditions.normalize();
     if (generate_automate->start!=nullptr){
       yy::parser::error (@$,"A start state was already choosen.");
     }
     State new_state($1, conditions);
     generate_automate->states.push_back(new_state);
     std::list<State>::iterator ite = generate_automate->states.end();
     ite--;
     generate_automate->start = &(*ite);
     generate_automate->transitions.insert(std::pair<State*, vector<Transition>>(generate_automate->start, {}));
   }
  |NAME END_NODE conditions {
    DBM conditions = $3;
    conditions.normalize();
    State new_state($1, conditions);
    generate_automate->states.push_back(new_state);
    std::list<State>::iterator ite = generate_automate->states.end();
    ite--;
    generate_automate->endStates.push_back(&(*ite));
    generate_automate->transitions.insert(std::pair<State*, vector<Transition>>(&(*ite), {}));
  }
  |NAME conditions {
    DBM conditions = $2;
    conditions.normalize();
    State new_state($1, conditions);
    generate_automate->states.push_back(new_state);
    std::list<State>::iterator ite = generate_automate->states.end();
    ite--;
    generate_automate->transitions.insert(std::pair<State*, vector<Transition>>(&(*ite), {}));
  }
  |NAME START_NODE END_NODE conditions {
    DBM conditions = $4;
    conditions.normalize();
    if (generate_automate->start!=nullptr){
      yy::parser::error (@$,"A start state was already choosen.");
    }
    State new_state($1, conditions);
    generate_automate->states.push_back(new_state);
    std::list<State>::iterator ite = generate_automate->states.end();
    ite--;
    generate_automate->start = &(*ite);
    generate_automate->endStates.push_back(&(*ite));
    generate_automate->transitions.insert(std::pair<State*, vector<Transition>>(generate_automate->start, {}));
  }
  |NAME END_NODE START_NODE conditions {
    DBM conditions = $4;
    conditions.normalize();
    if (generate_automate->start!=nullptr){
      yy::parser::error (@$,"A start state was already choosen.");
    }
    State new_state($1, conditions);
    generate_automate->states.push_back(new_state);
    std::list<State>::iterator ite = generate_automate->states.end();
    ite--;
    generate_automate->start = &(*ite);
    generate_automate->endStates.push_back(&(*ite));
    generate_automate->transitions.insert(std::pair<State*, vector<Transition>>(generate_automate->start, {}));
  }

arretes :
   arretes NLINE arrete { /*Nothing to do.*/ }
  |arrete { /*Nothing to do.*/ }

arrete :
  /*Ori*//*Dest*//*Events*//*Reset*/
   names  names    names    clocks  conditions {
     DBM conditions = $5;
     conditions.normalize();
     //Create the new transition for each pairs of origine, destination
     for(string ori : $1){
       State* origine_state = generate_automate->getState(ori);
       if(origine_state == nullptr){
         yy::parser::error (@$, "state undeclared : "+ori);
       }
       for(string dest : $2){
         State* destination_state = generate_automate->getState(dest);
         if(destination_state == nullptr){
           yy::parser::error (@$, "state undeclared : "+dest);
         }
         Transition new_trans(origine_state, destination_state, $3, conditions, $4);
         generate_automate->transitions[origine_state].push_back(new_trans);
       }
     }
     //Add new character to the automate alphabet.
     for(std::string const new_event : $3){
       generate_automate->alphabet.insert(new_event);
     }
   }
   /*Ori*//*Dest*//*Events*//*Reset*/
   |names  names    NONE    clocks  conditions {
      DBM conditions = $5;
      conditions.normalize();
      //Create the new transition for each pairs of origine, destination
      for(string ori : $1){
        State* origine_state = generate_automate->getState(ori);
        if(origine_state == nullptr){
          yy::parser::error (@$, "state undeclared : "+ori);
        }
        for(string dest : $2){
          State* destination_state = generate_automate->getState(dest);
          if(destination_state == nullptr){
            yy::parser::error (@$, "state undeclared : "+dest);
          }
          Transition new_trans(origine_state, destination_state, {}, conditions, $4);
          generate_automate->transitions[origine_state].push_back(new_trans);
        }
      }
    }
clocks :
   names {
     vector<Clock*> res;
     for(std::string clock_name : $1){
       res.push_back(generate_automate->find_or_create_clock(clock_name));
     }
     $$ = res;
   }
  | %empty { $$ = {}; }

names :
   LACOLADE list_name RACOLADE {$$ = $2;}
  |NAME { $$ = {$1};}

list_name :
   list_name COMMA NAME { $1.push_back($3); $$ = $1;}
  | NAME { $$ = { $1 };}

conditions :
   conditions condition {
     //TODO won't work!! Il va y avoir un out of bound exception -> dÃ©couverte de nouvelles horloges. Tu aurais du faire un ast grosse feignasse!!
    $1.matrice[$2.first->getId()][0] = $1.matrice[$2.first->getId()][0].min($2.second.second);
    $1.matrice[0][$2.first->getId()] = $1.matrice[0][$2.first->getId()].min($2.second.first);
    $$ = $1;
  }
  | %empty {
    $$ = DBM();
  }

condition :
   LCROCHET borne_inf NAME borne_sup RCROCHET {
     Clock* clk = generate_automate->find_or_create_clock($3);
     $$= std::pair<Clock*,std::pair<Bound,Bound>>(clk, std::pair<Bound,Bound>($2,$4));
   }
  |LCROCHET NAME borne_sup RCROCHET {
    Clock* clk = generate_automate->find_or_create_clock($2);
    $$= std::pair<Clock*,std::pair<Bound,Bound>>(clk, std::pair<Bound,Bound>(Bound(0),$3));
  }
  |LCROCHET NAME EGALE NUMBER RCROCHET {
    Clock* clk = generate_automate->find_or_create_clock($2);
    $$= std::pair<Clock*,std::pair<Bound,Bound>>(clk, std::pair<Bound,Bound>($4,$4));
   }

borne_inf :
   NUMBER LBALISE {
     $$= Bound(-1*$1, -1);
   }
  |NUMBER LBALISE EGALE {
    $$= Bound(-1*$1, 0);
  }

borne_sup :
  LBALISE NUMBER {
    $$= Bound($2, -1);
  }
  |LBALISE EGALE NUMBER {
    $$= Bound($3, 0);
  }
  |%empty{
    $$= Bound(); //Positiv infiny
  }


%%
void yy::parser::error (const yy::location& l,
                          const std::string& m)
{
  driver.error (l, m);
}
