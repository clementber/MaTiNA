%{
  #include <iostream>
  #include <string>
  #include "../fm_automata/grammar.hpp"
  #include "yacc.tab.hpp"
  using namespace automate;

  yy::parser::symbol_type yylex (yy::parser::semantic_type *arg);

%}
%skeleton "lalr1.cc"
%require "3.0.4"

%defines
%define parser_class_name {parser}
%define api.token.constructor
%define api.value.type variant
%define parse.trace
%define parse.error verbose

%parse-param {automate::Automate* generate_automate}

%code requires
{
  class autom_driver;
}
// The parsing context.
%param { autom_driver& driver }
%locations
%initial-action
{
  // Initialize the initial location.
  @$.begin.filename = @$.end.filename = &driver.file;
};
%code
{
# include "parsingDriver.hpp"
}

%token END 0 "end of file"
%token <int> NUMBER
%token <std::string> NAME

%type <automate::Automate*> autom
%type <std::vector<std::string>> names list_name
%type <std::vector<int>> nums list_num

%token COMMA LACOLADE RACOLADE
%token START_NODE END_NODE NLINE DOUBLE_SLASH NONE

%%
autom :
   nodes NLINE DOUBLE_SLASH NLINE arretes { $$ = generate_automate; }
  |nodes NLINE DOUBLE_SLASH NLINE arretes NLINE { $$ = generate_automate; }

nodes :
   nodes NLINE node { /*Nothing to do.*/ }
  |node { /*Nothing to do.*/ }

node :
   NAME START_NODE nums {
     if (generate_automate->start!=nullptr){
       yy::parser::error (@$,"A start state was already choosen.");
     }
     State new_state($1, $3);
     generate_automate->states.push_back(new_state);
     std::list<State>::iterator ite = generate_automate->states.end();
     ite--;
     generate_automate->start = &(*ite);
   }
  |NAME END_NODE nums {
    State new_state($1, $3);
    generate_automate->states.push_back(new_state);
    std::list<State>::iterator ite = generate_automate->states.end();
    ite--;
    generate_automate->endStates.push_back(&(*ite));
  }
  |NAME nums {
    State new_state($1, $2);
    generate_automate->states.push_back(new_state);
    std::list<State>::iterator ite = generate_automate->states.end();
    ite--;
  }
  |NAME START_NODE END_NODE nums {
    if (generate_automate->start!=nullptr){
      yy::parser::error (@$,"A start state was already choosen.");
    }
    State new_state($1, $4);
    generate_automate->states.push_back(new_state);
    std::list<State>::iterator ite = generate_automate->states.end();
    ite--;
    generate_automate->start = &(*ite);
    generate_automate->endStates.push_back(&(*ite));
  }
  |NAME END_NODE START_NODE nums {
    if (generate_automate->start!=nullptr){
      yy::parser::error (@$,"A start state was already choosen.");
    }
    State new_state($1, $4);
    generate_automate->states.push_back(new_state);
    std::list<State>::iterator ite = generate_automate->states.end();
    ite--;
    generate_automate->start = &(*ite);
    generate_automate->endStates.push_back(&(*ite));
  }

arretes :
   arretes NLINE arrete { /*Nothing to do.*/ }
  |arrete { /*Nothing to do.*/ }

arrete :
  /*Ori*//*Dest*//*Events*/
   names  names    nums {
     //Create the new transition for each pairs (origine, destination)
     for(string ori : $1){
       State* origine_state = generate_automate->getState(ori);
       if(origine_state == nullptr){
         yy::parser::error (@$, "state undeclared : "+ori);
       }
       for(string dest : $2){
         State* destination_state = generate_automate->getState(dest);
         if(destination_state == nullptr){
           yy::parser::error (@$, "state undeclared : "+dest);
         }
         Transition new_trans(origine_state, destination_state, $3);
         generate_automate->transitions[origine_state].push_back(new_trans);
       }
     }
   }
   /*Ori*//*Dest*//*Events*//*Reset*/
   |names  names    NONE {
      //Create the new transition for each pairs of origine, destination
      for(string ori : $1){
        State* origine_state = generate_automate->getState(ori);
        if(origine_state == nullptr){
          yy::parser::error (@$, "state undeclared : "+ori);
        }
        for(string dest : $2){
          State* destination_state = generate_automate->getState(dest);
          if(destination_state == nullptr){
            yy::parser::error (@$, "state undeclared : "+dest);
          }
          Transition new_trans(origine_state, destination_state, {});
          generate_automate->transitions[origine_state].push_back(new_trans);
        }
      }
    }

names :
   LACOLADE list_name RACOLADE {$$ = $2;}
  |NAME { $$ = {$1};}

list_name :
   list_name COMMA NAME { $1.push_back($3); $$ = $1;}
  | NAME { $$ = { $1 };}

nums :
   LACOLADE list_num RACOLADE {$$ = $2;}
  |NUMBER { $$ = {$1};}
  | {$$={};}

list_num :
   nums COMMA NUMBER { $1.push_back($3); $$ = $1;}
  |NUMBER { $$ = { $1 };}
%%
void yy::parser::error (const yy::location& l,
                          const std::string& m)
{
  driver.error (l, m);
}
